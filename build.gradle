apply plugin: 'com.android.library'

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

ext {
    groupOpen = false
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
        // to generate the required jar dependencies e.g. cling seamless
        preBuild.dependsOn(ndkBuild)
    }
}

configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
}

android {
    compileSdkVersion 33
    gradle.startParameter.getTaskNames().each { task ->
        if (task.contains("Open")) {
            groupOpen = true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //tmp prebuilt jni
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
	multiDexEnabled true
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    clean.dependsOn 'ndkClean'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/maven'
        exclude 'README'
    }
}

task ndkBuild(type:Exec) {
    workingDir = "../"
    List<String> myCmd = new ArrayList<String>()
    myCmd.add("make")
    myCmd.add("native_libyuv")
    myCmd.add("native_libnativehelper")
    myCmd.add("native_avos_full")
    myCmd.add("native_torrentd")
    //myCmd.add("trakt-java")
    myCmd.add("seamless")
    myCmd.add("cling")
    commandLine myCmd
}

task ndkClean(type:Exec) {
    workingDir = "../"
    List<String> myCmd = new ArrayList<String>()
    myCmd.add("make")
    myCmd.add("native_clean")
    //myCmd.add("external_clean_trakt-java")
    //myCmd.add("external_clean_seamless")
    //myCmd.add("external_clean_cling")
    commandLine myCmd
}

def useSan = false
if (useSan)
    apply from: 'profiling.gradle'

dependencies {
    implementation project(':FileCoreLibrary')
    //implementation 'com.android.support:support-v4:28.0.0'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.fragment:fragment:1.5.4'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.preference:preference:1.2.0'
    //implementation 'cz.msebera.android:httpclient:4.5.8'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.2'
    implementation 'org.json:json:20210307'
    // trakt depedencies: we imported the source code
    //api 'com.uwetrottmann.trakt5:trakt-java:6.1.0'
    //api files('libs/trakt-java.jar')
    implementation ('com.uwetrottmann.trakt5:trakt-java:6.11.2') {
        exclude group: 'org.threeten', module: 'threetenbp'
    }

    implementation 'org.threeten:threetenbp:1.6.1'

    api ('org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2') {
        exclude group:'org.apache.oltu.oauth2' , module: 'org.apache.oltu.oauth2.common'
    }

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.commons:commons-text:1.9'
    // version aligned with external dependencies trakt-java tmdb-java (check pom.xml)
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.uwetrottmann.tmdb2:tmdb-java:2.8.1'
    // official dependencies for cling 2.1.2 is https://github.com/4thline/cling/blob/master/distribution/src/dist/README.txt
    // no host discovery if switch from 2.1.1 to 2.1.2 supposed to be fixed in 2.1.1 https://github.com/4thline/cling/issues/164
    //api group: 'org.fourthline.cling', name: 'cling-core', version:'2.1.1'
    //implementation group: 'org.fourthline.cling', name: 'cling-support', version:'2.1.1'
    // investigate https://mvnrepository.com/artifact/org.jupnp/jupnp and https://github.com/UniversalMediaServer/UniversalMediaServer/pull/2789
    //implementation 'org.jupnp:jupnp:2.6.0'
    api files('libs/cling-core-2.1.2.jar');
    implementation files('libs/cling-support-2.1.2.jar');
    implementation files('libs/seamless-util-1.1.2.jar');
    implementation files('libs/seamless-http-1.1.2.jar');
    implementation files('libs/seamless-xml-1.1.2.jar');
    implementation files('libs/seamless-swing-1.1.2.jar');
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.22.v20160922'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.1.22.v20160922'
    implementation group: 'org.eclipse.jetty', name: 'jetty-client', version:'8.1.22.v20160922'
    implementation 'io.sentry:sentry:6.4.3'

}

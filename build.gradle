apply plugin: 'com.android.library'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://4thline.org/m2'
    }
    maven { url 'https://jitpack.io' }
}

ext {
    groupOpen = false
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    useLibrary 'org.apache.http.legacy'

    gradle.startParameter.getTaskNames().each { task ->
        if (task.contains("Open")) {
            groupOpen = true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //tmp prebuilt jni
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }
    publishNonDefault true
    flavorDimensions "version", "privlibs"
    productFlavors {
        normal {
            dimension "version"
        }
        free {
            dimension "version"
        }
        community {
            dimension "version"
        }
        open {
            dimension "privlibs"
        }
        privlibs {
            dimension "privlibs"
        }

    }

    task bootstrapFFmpeg(type:Exec) {
       workingDir = "../native/ffmpeg-android-builder"
       List<String> myCmd = new ArrayList<String>()
       myCmd.add("bash")
       myCmd.add("bootstrap_avp_ffmpeg.sh")
       commandLine myCmd
    }

    task ndkBuild(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_libyuv")
        myCmd.add("native_libnativehelper")
        myCmd.add("native_avos_base")
        if(!groupOpen)
            myCmd.add("native_torrentd")
        commandLine myCmd
    }

    ndkBuild.dependsOn bootstrapFFmpeg

    task ndkClean(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_clean")
        commandLine myCmd
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    clean.dependsOn 'ndkClean'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/maven'
        exclude 'README'
    }
}

dependencies {
    compile project(':FileCoreLibrary')
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.android.support:multidex:1.0.1'
    compile 'joda-time:joda-time:2.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.8'
    compile group: 'org.joda', name: 'joda-convert', version: '1.7'
    compile group: 'org.apache.oltu.oauth2', name: 'org.apache.oltu.oauth2.client', version: '1.0.0'
    compile group: 'org.apache.oltu.oauth2', name: 'org.apache.oltu.oauth2.common', version: '1.0.0'
    privlibsCompile files('libs/in-app-purchasing-2.0.76.jar')
    compile group: 'org.json', name: 'json', version: '20131018'
    compile group: 'com.squareup.retrofit', name: 'retrofit', version: '1.8.0'
    compile group: 'com.googlecode.juniversalchardet', name: 'juniversalchardet', version: '1.0.3'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.12.v20130726'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.1.12.v20130726'
    compile group: 'org.eclipse.jetty', name: 'jetty-client', version:'8.1.12.v20130726'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version:'1.7.5'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.uwetrottmann.thetvdb-java:thetvdb-java:1.5.0'
    openCompile 'com.github.archos-sa:aos-FakeLibs:master-SNAPSHOT'
    privlibsCompile group: 'org.fourthline.cling', name: 'cling-core', version:'2.0.1'
    privlibsCompile group: 'org.fourthline.cling', name: 'cling-support', version:'2.0.1'
}

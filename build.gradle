apply plugin: 'com.android.library'

repositories {
    google()
    mavenCentral()
    mavenLocal()
    //maven { url 'https://mvnrepository.com/artifact' }
    maven { url 'http://4thline.org/m2' }
}

ext {
    groupOpen = false
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    useLibrary 'org.apache.http.legacy'

    gradle.startParameter.getTaskNames().each { task ->
        if (task.contains("Open")) {
            groupOpen = true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //tmp prebuilt jni
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
    }

    flavorDimensions "version"

    productFlavors {
        community {
            dimension "version"
        }
    }

    task ndkBuild(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_libyuv")
        myCmd.add("native_libnativehelper")
        myCmd.add("native_avos_full")
        myCmd.add("native_torrentd")
        commandLine myCmd
    }

    task ndkClean(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_clean")
        commandLine myCmd
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    clean.dependsOn 'ndkClean'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/maven'
        exclude 'README'
    }
}

dependencies {
    implementation project(':FileCoreLibrary')
    //implementation 'com.android.support:support-v4:28.0.0'
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.media:media:1.0.1'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation group: 'org.json', name: 'json', version: '20180813'
    // trakt depedencies: we imported the source code
    //api 'com.uwetrottmann.trakt5:trakt-java:6.1.0'
    implementation 'joda-time:joda-time:2.10'
    implementation group: 'org.joda', name: 'joda-convert', version: '1.9.2' 
    api group: 'org.apache.oltu.oauth2', name: 'org.apache.oltu.oauth2.client', version: '1.0.0'
    api group: 'org.apache.oltu.oauth2', name: 'org.apache.oltu.oauth2.common', version: '1.0.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.11' 
    implementation group: 'com.squareup.retrofit', name: 'retrofit', version: '1.9.0'
    // thetvdb depedencies (stuck with 1.6.1)
    //implementation 'com.uwetrottmann.thetvdb-java:thetvdb-java:2.1.0'
    implementation 'com.uwetrottmann.thetvdb-java:thetvdb-java:1.6.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    // official dependencies for cling 2.1.2 is https://github.com/4thline/cling/blob/master/distribution/src/dist/README.txt
    // no host discovery if switch from 2.1.1 to 2.1.2 supposed to be fixed in 2.1.1 https://github.com/4thline/cling/issues/164
    api group: 'org.fourthline.cling', name: 'cling-core', version:'2.1.1'
    implementation group: 'org.fourthline.cling', name: 'cling-support', version:'2.1.1'
    // official version of cling jetty depedency is 8.1.22.v20160922, pushed jetty to latest 8.2.x version but it is now 9.4.x
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.2.0.v20160908'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.2.0.v20160908'
    implementation group: 'org.eclipse.jetty', name: 'jetty-client', version:'8.2.0.v20160908'
    implementation group: 'org.slf4j', name: 'slf4j-jdk14', version:'1.7.25'
}
